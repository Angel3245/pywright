label court
clearcourt
set _defense Apollo
set _prosecutor Poole
set _judge G.Strings
set _helper nothing
set _witness Rhea
startcourt

witness
pause 60

prosecutor
"Please state your name{n}and occupation."
witness
"As Apollo just mentioned, it says{n}'Rhea Wits, Funeral Director'{n}on my business card."
judge
"And on that night the defendant{n}tried to kill you, and then you{n}saw him murder the victim?"
witness
"Y-{p15}{s 5 5}Yeah...{p30} I-{p15}{s 5 5}It's hard to think{n}about, but... {p30}{emad}but I {f}have to do{n}what's right!"
judge
"How brave you are!{p20} Please don't{n}push her too hard, Mr. Justice."
defense
"O-{p15}Okay, Your Honor.{p30}{n}{esweats-nobrac}{c089}(He's eating out of{n}the palm of her hand!)"
judge
"Now then, please give the{n}court your testimony of what{n}happened that night."
witness
"A-{p15}{f}All right..."

# RHEA'S STATEMENT
witness
fg TestimonyAnim fade name=thatstupidanim
delete name=thatstupidanim
nt
"{n}{c900}{center}-- What I Witnessed --"
mus 111 - Apollo Justice - Examination moderate.ogg
set _music_loop 111 - Apollo Justice - Examination moderateloop.ogg
witness
"{tbon}I snuck out of Apollo's office{n}that night to get something at{n}my place."
"I was walking along the street{n}when I was struck from behind{n}and lost consciousness."
"When I woke up, I was in{n}Forest Bones Cemetery."
emo surprised name=wc
"I watched in fear as Judge{n}and Erlenmeyer fought!"
"Erlenmeyer knocked Judge{n}unconscious against the car."
emo sweating name=wc
"The impact set off the car{n}alarm... The noise was terrifying!"
emo normal name=wc
"Panicked, Erlenmeyer paced up and{n}down several times, and then{n}decided to steal the car keys."
emo mad name=wc
"Erlenmeyer then ran over Judge{n}with the car several times."
"Erlenmeyer passed out when he{n}stepped out of the car and saw{n}what he had done."
"This gave me an opportunity{n}to escape."
emo normal name=wc
"I came back to your office on foot{n}like nothing happened. {esmiling1}I was so{n}relieved to be back in your arms!"

mus
prosecutor
"How {f}ironic.{p30} You accuse your own{n}assistant, and then get decisive{n}evidence that your client is"
"guilty as a result.{p20} Even I feel{n}a little bad for you, Antennas.{p20}{n}{esarcastic}{delay2}But just a little."
defense
emo handsondesk-nobrac name=dc
"{c089}(Don't {s 5 5}cry for me, Mrs. Poole!{n}The truth is I'll {f}never give up!)"
"{c089}(Pity Rhea, since I'm going{n}to {s 8 8}expose her for what she{n}really is!)"
judge
"Her testimony seems fairly{n}solid, yet you seem strangely{n}confident...{p30} for once."
"Let's see if you are justified{n}in your demeanor:{p20} you may begin{n}your cross-examination."


# SETTING UI FOR CROSS EXAMINATION
set wrongevidence1

# BEGIN CROSS EXAMINATION
witness
fg Cross-ExaminationAnim fade
nt
"{n}{c900}{center}-- What I Witnessed --"
mus 111 - Apollo Justice - Examination moderate.ogg
set _music_loop 111 - Apollo Justice - Examination moderateloop.ogg
label start
startcourt
cross rheatestimony1

statement rhea1A
witness
emo normal name=wc
"I snuck out of Apollo's office{n}that night to get something at{n}my place."

statement rhea1B
witness
emo normal name=wc
"I was walking along the street{n}when I was struck from behind{n}and lost consciousness."

statement rhea1C
witness
emo normal name=wc
"When I woke up, I was in{n}Forest Bones Cemetery."

statement rhea1L test=addRhea1L
witness
emo sad name=wc
"I was transported in the{n}trunk of Erlenmeyer's car{n}so he could kill me there."

statement rhea1D
witness
emo surprised name=wc
"I watched in fear as Judge{n}and Erlenmeyer fought!"

statement rhea1M test=addRhea1M
witness
emo normal name=wc
"They couldn't see me since I{n}was hiding behind a tombstone{n}several feet away."

statement rhea1E
witness
emo mad name=wc
"Erlenmeyer knocked Judge{n}unconscious against the car."

statement rhea1F
witness
emo sweating name=wc
"The impact set off the car{n}alarm... The noise was terrifying!"

statement rhea1G
witness
emo normal name=wc
"Panicked, Erlenmeyer paced up and{n}down several times, and then{n}decided to steal the car keys."

statement rhea1H
witness
emo mad name=wc
"Erlenmeyer then ran over Judge{n}with the car several times."

statement rhea1I
witness
emo mad name=wc
"Erlenmeyer passed out when he{n}stepped out of the car and saw{n}what he had done."

statement rhea1J
witness
emo normal name=wc
"This gave me an opportunity{n}to escape."

statement rhea1K
witness
emo normal name=wc
"I came back to your office on foot{n}like nothing happened. {esmiling1}I was so{n}relieved to be back in your arms!"
goto comments
endcross

# PRESS POINTS

label press rhea1A
fg HoldItAnimApollo
pause 5 script
defense
emo thinking-nobrac name=dc
"You didn't spend the night...{p30}{n}just {f}so I wouldn't suspect you,{n}did you?"
judge
emo shocked name=jc
"You {sfx/Shock.ogg}{s}slept with your own{n}assistant?!"
defense
emo ohshit-nobrac name=dc
"{p15}{esweats-nobrac}{s 8 8}Sh-She just spent the night!{p30}{n}We didn't..."
prosecutor
emo normal name=pc
"I was under the impression you had{n}only recently met. {esarcastic}I didn't think{n}you were the type to be such a"
"Don Juan, Lady Justice. {eserious}But still{n}you then must consider: aren't you{n}a bit {sfx/Key.ogg}{f}{c900}emotionally biased{c} for"
"accusing this woman?"
witness
emo normal name=wc
"Mrs. Poole, I think I can answer.{n}Polly, if there's one thing I{n}want you to know, it's...{p15}"
"I genuinely {sfx/Ding.ogg}{f}like {p10}you.{p30} I sincerely{n}don't want this trial to destroy{n}the bond we have."
"By the way, what I was getting at{n}my office...{p40} it was a {sfx/Key.ogg}{f}{esad}present.{p20}{n}{c900}For {f}{s 5 5 }you{c}, Polly."
emo wink name=wc
pause 40 script
defense
emo handsondesk-nobrac name=dc
"You... {p30}You... {p30}{esweats-nobrac}{sfx/Shock.ogg}{s 10 10}Argh!"
prosecutor
emo sarcastic name=pc
"{f}'Argh!' indeed."
judge
emo serious name=jc
"You have a strange way of{n}thanking women for their{n}affection, Mr. Justice."
"You may continue your testimony,{n}Ms. Wits."
resume

label press rhea1B
fg HoldItAnimApollo
pause 5 script
defense
emo normal-nobrac name=dc
"Did you see this attacker's face?"
witness
emo normal name=wc
"Don't be so {s 5 5}silly, {s 5 5}silly! I was{n}struck from behind, and {esmiling2}{f}even you{n}should be able to tell where my"
"only two eyes are. {enormal}I can only{n}guess it was Erlenmeyer, though."
defense
emo normal-nobrac name=dc
"I find it a {sfx/Key.ogg}{f}little strange that{n}I haven't seen any signs of you{n}being struck."
witness
emo mad name=wc
"I was struck in the back of my{n}head. I thought that was a bit{n}obvious, since I was struck"
"from behind, but apparently {sfx/Ding.ogg}{f}not{n}to everyone.{p20} {esmiling2}Anyway, I'm so{n}sorry I have hair."
judge
emo normal name=jc
"Is there anything else you{n}want to ask, Mr. Justice?"
defense
emo thinking-nobrac name=dc
"N-{p15}No...{p30} Not {f}yet, at least..."
judge
emo normal name=jc
"Then please continue, witness."
resume

label press rhea1C
flag addRhea1L altRhea1C
fg HoldItAnimApollo
pause 5 script
defense
emo normal-nobrac name=dc
"How and why were you transported{n}there?"
witness
emo normal name=wc
"...{p30}In case you've forgotten,{n}{delay2}I was {f}unconscious at the time."
defense
emo sheepish-nobrac name=dc
"W-{p15}Well, {p20}{enormal-nobrac}what's your opinion on{n}how and why?"
witness
emo normal name=wc
"Hm...{p30} I think I was transported in{n}the {f}trunk of Erlenmeyer's car.{p30} {emad}He{n}probably thought it was an ideal"
"place to finish me off."
judge
emo normal name=jc
"I would say those are important{n}details. Please add them to your{n}testimony."
setflag addRhea1L
resume
# AFTER PRESSING THIS POINT ONCE
label altRhea1C
fg HoldItAnimApollo
pause 5 script
defense
emo normal-nobrac name=dc
"Did you understand what was{n}going on when you woke up?"
witness
emo sad name=wc
"Y-{p20}{s 5 5}Yeah...{p30} I was {s 8 8}{f}so scared!{n}I thought I was going to...{p30}{n}{s 8 8}{f}going to die!"
"I-{p20}{s 5 5}I was even too scared{n}to call for help..."
judge
emo normal name=jc
"Yes, that's understandable.{n}{eserious}Mr. Justice,{sfx/Smack.ogg} {s 8 8}please hurry up{n}with your cross-examination"
"so we can get this poor girl{n}off the stand!"
defense
emo sweats-nobrac name=dc
"{c089}(Please, Your Honor, try to{n}retain an {f}air of nonpartiality!)"
resume

label press rhea1L
fg HoldItAnimApollo
pause 5 script
flag carContradictionFound altRhea1L
defense
emo normal-nobrac name=dc
"Why would he want to kill you{n}in particular?"
witness
emo normal name=wc
"I was walking alone at night. He{n}probably thought I was easy prey."
prosecutor
emo normal name=pc
"...{p30}This is also a deranged {s 5 5}madman{n}we're talking about. He could try{n}and kill anyone, for any reason."
judge
emo normal name=jc
"Yes...{p30} Please go on, Ms. Wits."
resume
# PRESENT JUDGE'S CAR ON THIS POINT
label judgescar_rhea1L
fg ObjectionAnimApollo
pause 5 script
scrolldefense
emo objects-nobrac name=dc
pause 7 script
mus
"{p20}{enormal-nobrac}You said Erlenmeyer carried you in{n}the {f}trunk of his car, but that{n}{sfx/Shing.ogg}{f}{s 10 10}doesn't make sense!"
"Chambers was run over by his {f}own{n}car.{p30} If Erlenmeyer had his own{n}car at the crime scene, {s 8 8}why waste"
mus 113 - Apollo Justice - Objection.ogg
set _music_loop 113 - Apollo Justice - Objectionloop.ogg
"time stealing the {f}victim's keys{n}when he already had a set for his {sfx/Ding.ogg}{f}{n}own car? And that's not all...{p30}"
"He was found unconscious at the{n}cemetery.{p20} That means that his car{n}{c900}should {s 8 8}still be there{c}, but only"
"{sfx/Shing.ogg}{s 10 10}{f}Chambers' car was found!"
mus
witness
emo mad name=wc
"...{p30}{sfx/Key.ogg}{f}Please, Apollo.{p20} I was only{n}making a guess, not saying that{n}actually happened.{p20} {f}You're the one"
"who told me to do that.{p30} {enormal}Also, the{n}cemetery isn't that small. If{n}Erlenmeyer had parked in the"
"regular parking lot, it would've{n}been {f}quicker just to steal Judge's{n}keys.{p20} Maybe Erlenmeyer's car is"
"{f}still in that parking lot.{p20} You{n}gotta remember that he was in a{n}{s 5 5}rush because of the alarm."
judge
emo normal name=jc
"Yes, I believe that is a{n}sensible way to clear up this{n}contradiction. Since it is"
"only the witness' guess, I do{n}not believe we need to dwell{n}on it so much."
"You may continue with your{n}testimony."
defense
emo normal-nobrac name=dc
"{c089}(I {s 8 8}almost had her! I need{n}something more concrete{n}than just her guess...)"
setflag carContradictionFound
flag carContradictionFound AND gumContradictionFound AND wheelContradictionFound OnceMoreWithFeeling
mus 111 - Apollo Justice - Examination moderate.ogg
set _music_loop 111 - Apollo Justice - Examination moderateloop.ogg
resume

# PRESS AFTER FINDING CONTRADICTION
label altRhea1L
defense
emo headshakes-nobrac name=dc
"{p15}{enormal-nobrac}...{p30}{sfx/Ding.ogg}I'm still not convinced{n}Erlenmeyer drove his car to the{n}cemetery.{p20} {f}How did he get it"
"in the first place?{p20} It had to be{n}near his home...{p30} where the{n}{f}{s 8 8}police checked first!"
prosecutor
emo normal name=pc
"We've been over this.{p20} Erlenmeyer{n}was simply quicker than the{n}police. Nothing more to it."
judge
"And we don't even know if he drove{n}his own car.{p20} Perhaps he stole{n}someone else's."
"Besides, I believe what Ms. Wits{n}actually witnessed is a bit more{n}important than how she thinks"
"she got to the cemetery."
resume

label press rhea1D
fg HoldItAnimApollo
pause 5 script
defense
emo normal-nobrac name=dc
"{c089}(She's being vague... I need to{n}{s 5 5}press her into details if I want{n}to {f}catch her on something!)"
set _list_back_button false
# LIST OPTIONS TO PRESS
list pressRhea1D
li Where were they fighting?
li Where were you standing?
li When did they start fighting?
showlist
# WHERE WERE THEY FIGHTING?
label Where were they fighting?
set _list_back_button true
defense
emo normal-nobrac name=dc
"Where were the victim and{n}defendant fighting?"
witness
emo normal name=wc
"They were pretty close to{n}Judge's car...{p30}{n}Maybe {sfx/Ding.ogg}{f}five or six feet away?"
defense
"I see...{p20} so what did you do next?"
setflag AskedAboutFighting
resume
# WHEN DID THEY START FIGHTING?
label When did they start fighting?
set _list_back_button true
defense
emo normal-nobrac name=dc
"When did the fight start?"
witness
emo normal name=wc
"Before I woke up. Which makes{n}sense if the reason they were{n}fighting was because Judge"
"saw Erlenmeyer carrying me."
defense
"True...{p30} Then what happened?"
resume
# WHERE WERE YOU STANDING?
label Where were you standing?
set _list_back_button true
flag addRhea1M altRhea1M
defense
emo normal-nobrac name=dc
"The other day, you gave me this{n}tip...{p20} To imagine what it would{n}be like if a {f}second witness"
"were present.{p20} Sometimes a witness{n}is not just a neutral, outside{n}observer, but {s 8 8}influences the"
"events that occur, even if it is{n}just with his or her presence..."
"So {f}where were you watching this{n}struggle from?"
"{c089}(Obviously somewhere hidden...{p20}{n}Otherwise they'd have reacted{n}to her being there...)"
witness
emo normal name=wc
"{delay2}Oh, right... {delay1}I was hiding behind a{n}tombstone, a few feet away from{n}them, so they couldn't see me."
"{esad}I-{p15}{s 8 8}I was scared for my own life,{n}and shocked to see Judge killed..."
"S-{p15}{s 5 5}Sorry, I knew I had forgotten{n}something."
judge
emo normal name=jc
"It's okay, we understand.{n}Please amend your testimony{n}with this new information."
setflag addRhea1M
resume
# WHERE WERE YOU STANDING 2ND+ TIME
label altRhea1M
defense
emo normal-nobrac name=dc
"So where were you when{n}this was happening?"
resume

label press rhea1M
fg HoldItAnimApollo
pause 5 script
defense
emo normal-nobrac name=dc
"Oh, {sfx/Smack.ogg}{s 10 10}come on! This isn't something{n}you just forget!{p20} It's more like{n}you {f}made it up on the spot."
fg ObjectionAnimPoole
pause 5 script
prosecutor
emo normal name=pc
"This girl was {f}{s 8 8}traumatized by{n}seeing a good friend killed{n}and was almost {f}{s 8 8}murdered herself!"
"Do you think you could remember{n}every detail after such stress{n}and shock?"
judge
emo normal name=jc
"Yes...{p30} My father always said,{n}'The witnesses who give {f}perfectly{n}detailed, {f}chronological"
"testimonies are the ones who are{n}most likely lying.'"
defense
"O-{p15}Okay, fine, let's move on...{p20}{n}{c089}(Before he starts talking like{n}his father even more...)"
resume
# PRESENT GUM AND ASKED WHERE THEY WERE FIGHTING
label gum_rhea1M
noflag AskedAboutFighting noDice
fg ObjectionAnimApollo
pause 5 script
mus
defense
emo normal-nobrac name=dc
"'Several feet away,' huh?{p30}{n}{eheadshakes-nobrac}{p11}{enormal-nobrac}If {sfx/Key.ogg}{f}only that were true."
witness
emo mad name=wc
"It is.{p20} I think I'd know {s 8 8}where I'm{n}standing better than you, Apollo."
defense
"{sfx/Ding.ogg}{f}Apparently not.{p20} You just testified{n}that the struggle occurred near{n}the car, and that you were several"
"feet away from the struggle,{n}right?"
witness
"Is there an {s 8 8}echo in here{n}or something?"
defense
"...{p30}I'll take that as a {f}yes.{p20}{n}Anyway, that means that you had{n}to be {f}{s 8 8}several feet away from"
"the car, too!"
prosecutor
"Get to the point, Antennas."
sfx /evidenceshoop.wav
ev gum name=gum x=185
defense
emo document-nobrac name=dc
"You remember this {f}gum, don't you?{p20}{n}It doesn't just prove that you{n}were at the crime scene..."
sfx /evidenceshoop.wav
delete name=gum
mus 115 - Apollo Justice - Tell the Truth.ogg
set _music_loop 115 - Apollo Justice - Tell the Truthloop.ogg
"It shows {s 8 8}{f}where in the crime scene{n}you stood! The gum was found near{n}the car, which means you were"
"standing near the car...{p30} You{n}weren't {f}{s 5 5}observing the struggle,{n}{ehandsondesk-nobrac}you were {s 10 10}{f}{c900}participating{c} in it!"
"{sfx/Shing.ogg}{eobjects-nobrac}{p7}{epointing-nobrac}Rhea, {f}{s 12 12}you're the one who assaulted{n}Chambers and {sfx/Dunnn.ogg}{f}{s 12 12}you're the one who{n}killed him!"
"It's the only {f}{c900}reasonable{n}explanation{c}!"
witness
"No, it's not.{p30} I honestly didn't{n}think you'd {f}treat me like this,{n}but I guess it just shows you"
"how {f}desparate you are.{p30} I thought{n}you'd value our {sfx/Ding.ogg}{f}relationship more{n}to not grasp this, but I"
"was wrong.{p30} {esmiling2}Rookie attorneys...{p30}{n}{sfx/Key.ogg}{f}what are you going to do with{n}them, am I right, Poole?"
defense
emo handsondesk-nobrac name=dc
"{p5}Explanation. {sfx/Table Slam.ogg}{edeskslam-nobrac}{s 8 8}{p16}Now."
mus
witness
emo normal name=wc
"All right, all right.{p30} I didn't{n}think I had to {f}spell it out for{n}you."
"How about when Erlenmeyer{n}walked through that spot while{n}carrying me, {f}before Judge came? 
"The chewing gum probably fell{n}out of my pocket, {sfx/Ding.ogg}{f}by chance,{n}at that moment."
defense
emo normal-nobrac name=dc
"Now wouldn't that be a{n}{f}wonderful coincidence?"
judge
"Maybe, but you can't disprove it.{p20}{n}The gum shows she was at that spot{n}at a certain time, but we"
"don't know {f}what time it was."
prosecutor
emo serious name=pc
"{s 5 5}Don't you get it yet, Lady{n}Justice?{p20} That {sfx/Smack.ogg}{s 8 8}chewing gum{n}can only get you so far!"
"You can argue about possible{n}{f}'where's and {f}'how's all you want,{n}but the evidence only links"
"{f}Erlenmeyer to the actual crime!{p20}{n}Now please stop {s 5 5}badgering the{n}witness with pointless remarks."
defense
emo thinking name=dc
"{c089}(Poole has not been her usual self{n}since I accused Rhea.{p20} Could she{n}be...{p30} {f}doubting her case?)"
"{c089}(Strings on the other hand seems{n}oblivious to how suspicious Rhea{n}is, and always questions my logic...)"
"{c089}{esweats-nobrac}(Does he have a {s 5 5}crush on her or{n}something?{p20} So much for staying{n}unbiased.)"
setflag gumContradictionFound
flag carContradictionFound AND gumContradictionFound AND wheelContradictionFound OnceMoreWithFeeling
mus 111 - Apollo Justice - Examination moderate.ogg
set _music_loop 111 - Apollo Justice - Examination moderateloop.ogg
resume

label noDice
goto none

label press rhea1E
fg HoldItAnimApollo
pause 5 script
defense
emo normal-nobrac name=dc
"How did that happen, exactly?"
witness
emo normal name=wc
"Erlenmeyer was stronger than{n}Judge, and slowly {s 8 8}pushed him{n}back towards the car..."
"Once Judge couldn't go back any{n}farther, Erlenmeyer grabbed his{n}robe and {sfx/sfx-thud4.wav}{s 8 8}knocked him against"
"the car."
judge
emo normal name=jc
"What happened next?"
resume

label press rhea1F
fg HoldItAnimApollo
pause 5 script
defense
emo normal-nobrac name=dc
"Are you absolutely sure the{n}shock set off the car?"
witness
emo normal name=wc
"As sure as I can be of anything.{p20}{n}The alarm went off at the {f}exact{n}same moment, and it didn't seem"
"like anything else happened to{n}set off the alarm."
defense
"But what if Chambers set off{n}the alarm {sfx/Key.ogg}{f}himself?"
witness
"Um...{p20} {sfx/Ding.ogg}{f}how?"
defense
"I don't know...{p30} Maybe there{n}was a {sfx/Ding.ogg}{f}button he could press?"
prosecutor
emo normal name=pc
"I've read all the police reports,{n}and examined the car myself.{p20}{n}There is {f}no such button."
witness
"What would it even change?"
defense
"If he was able to do that, he{n}could have also left us a dying{n}message, {f}{s 8 8}identifying the killer!"
"One we haven't found yet!"
witness
"...{p30}True, but it was just the{n}shock that set off the alarm,{n}so I doubt it."
judge
emo normal name=jc
"I believe it is much easier to{n}assume the impact did set off{n}the alarm.{p20} What happened next?"
resume

label press rhea1G
fg HoldItAnimApollo
pause 5 script
defense
emo normal-nobrac name=dc
"Weren't there {sfx/Key.ogg}{f}better{n}options available?"
witness
emo normal name=wc
"Better options like {f}what?"
defense
"Well, {s 5 5}killing Judge, {s 5 5}getting rid{n}of the body, and {s 5 5}escaping before{n}the police arrived would have"
"been an impossible feat.{p20} {sfx/Ding.ogg}{f}Why{n}didn't he just escape?{p15} Judge would{n}be able to testify against him,"
"but by then he could be{n}miles away."
witness
"Of all people, do you really think{n}{s 8 8}Robert Erlenmeyer would be able to{n}make a rational decision like"
"that? He looked terrified...{p30}{n}Most sane people probably wouldn't{n}think like that."
defense
"I suppose, but still: one more{n}testimony {s 8 8}{f}wouldn't change anything{n}for Erlenmeyer, as he'd still be a"
"serial killer on the loose!{n}However, no one has suspected {f}you{n}until now...{p30} If you let Judge live"
"after he witnessed you trying to{n}kill Erlenmeyer, it would {sfx/Smack.ogg}{s 10 10}ruin{n}everything you've done!"
witness
"So if you assume I'm the killer...{p30}{n}{esmiling2}it {sfx/Ding.ogg}{f}logically follows I'm the{n}killer. {delay2}Brilliant."
defense
"Uh..."
witness
emo normal name=wc
"That argument can be used for{n}anyone here...{p30} {f}{s 8 8}Even you!{p20} If Judge{n}had seen {f}you, Apollo, try to kill"
"Erlenmeyer, his testimony would've{n}{sfx/Shing.ogg}{f}{s 10 10}ruined your life!{p20} I suppose that{n}does sound convincing..."
"{emad}But I never thought you'd try to{n}use such {sfx/Smack.ogg}{s 10 10}underhanded tactics,{n}{s 8 8}especially against me!"
judge
emo normal name=jc
"He's a defense attorney.{p20}{n}{f}What do you expect?"
defense
"{esweats-nobrac}{c089}(Wh-{p15}{sfx/Shock.ogg}{s 10 10}What?!{p20} Can you at least{n}{s 8 8}try to {c900}seem{c} unbiased?)"
judge
"Anyway, please continue."
resume

label press rhea1H
fg HoldItAnimApollo
pause 5 script
flag wheelContradictionFound DontBelieveIt
defense
emo thinking-nobrac name=dc
"{f}Wait a moment...{p30} If Chambers had{n}been {s 8 8}knocked unconscious against{n}the car, then he had to be"
"extremely close to the car...{p30}{n}{enormal-nobrac}Meaning there wasn't {s 8 8}enough room,{n}and thus {s 8 8}speed, to {f}run him over!"
"This means that Erlenmeyer {f}{s 10 10}had to{n}have moved the body first!{p30} This{n}is also shown by the fact that"
"the body was found several feet{n}from the car."
prosecutor
"But wouldn't he have left{n}{sfx/Ding.ogg}{f}fingerprints if that were{n}the case?{p20} Obviously, if"
"Erlenmeyer's fingerprints were{n}found on Chambers' robe, I{n}would've {f}mentioned it in the"
"beginning!"
defense
"{sfx/Ding.ogg}{f}Exactly.{p20} Rhea has been talking{n}about a {s 8 8}struggle between{n}Erlenmeyer and the victim, but"
"we haven't found any {f}{s 10 10}fingerprints{n}on his clothes! This makes no{n}sense--{p20}{next}"
scrollwitness
emo normal name=wc
"Unless Erlenmeyer was wearing{n}{sfx/Key.ogg}{f}gloves.{p20} I can't say I remember{n}that he {f}wore them...{p30} but I don't"
"remember that he {f}didn't. {esad}I-{p20}{s 5 5}I'm{n}sorry, I didn't get a good look{n}at his hands..."
judge
emo normal name=jc
"It's okay, no one expects you{n}to have seen everything in a{n}situation like that!"
"...{p30}Although it does seem to be the{n}only logical explanation. What do{n}you think, Mr. Justice?"
defense
emo thinking-nobrac name=dc
"{c089}(Hm...{p30} What do I think about{n}Rhea's theory, {f}'Erlenmeyer{n}was wearing gloves?')"
# LIST OPTIONS TO PRESS
set _list_back_button false
list pressRhea1H
li It's fine
li It's contradictory
showlist
# IT'S FINE
label It's fine
set _list_back_button true
defense
emo normal name=dc
"Yeah, I suppose that would{n}make sense."
judge
"What happened next?"
resume
# IT'S CONTRADICTORY
label It's contradictory
set _list_back_button true
defense
mus
"Erlenmeyer {f}{s 8 8}couldn't have been{n}wearing gloves!{p20} It contradicts{n}{f}{s 8 8}this piece of evidence...!"
# PRESENT EVIDENCE
set _cr_back_button false
present fail=TimeWaster
# CORRECT ANSWER
label carphoto
set _cr_back_button true
mus
fg TakeThatAnimApollo
pause 5 script
defense
emo normal-nobrac name=dc
"If Erlenmeyer had been wearing{n}gloves, he wouldn't have left{n}{sfx/Shing.ogg}{f}{s 10 10}fingerprints on the {f}steering"
mus 113 - Apollo Justice - Objection.ogg
set _music_loop 113 - Apollo Justice - Objectionloop.ogg
"wheel, and the {f}car keys!{p20} Rhea, {s 8 8}you{n}are the one who fought with Judge{n}and ran him over, but you were"
"wearing gloves, so your{n}{s 8 8}fingerprints didn't get on{n}anything!{p30} {epointing-nobrac}You then {s 8 8}planted"
"{f}Erlenmeyer's fingerprints on the{n}keys and steering wheel, but in{n}your rush {s 8 8}forgot to plant them"
"on {f}Chambers' robe!"
mus
witness
emo normal name=wc
"{sfx/Ding.ogg}{f}Why do you always jump to{n}conclusions, Apollo?{p20} What{n}if Erlenmeyer took off his"
"gloves before taking the keys?"
defense
emo normal-nobrac name=dc
"And just {s 5 5}why would he do that?"
witness
"It was much easier to take and{n}handle the small keys with the{n}gloves off.{p20} Then he just forgot"
"to put them on when driving the{n}car. {p15}...{s}O-{p15}{sfx/Ding.ogg}{f}{s 5 5}Oh!{p15} I had forgotten!{n}{p15}I didn't see him take them off,"
"but I saw him place {sfx/Key.ogg}{f}gloves in his{n}pocket before he took the keys.{p20}{n}{delay1}Yeah... {esmiling2}{delay2}No doubt about it."
judge
emo normal name=jc
"I see. It seems that contradiction{n}wasn't decisive after all."
defense
emo sweats-nobrac name=dc
"{c089}({s 10 10}What?!{p20} She's just {f}making this{n}up as she goes along!)"
judge
"Please continue, Ms. Wits."
setflag wheelContradictionFound
flag carContradictionFound AND gumContradictionFound AND wheelContradictionFound OnceMoreWithFeeling
mus 111 - Apollo Justice - Examination moderate.ogg
set _music_loop 111 - Apollo Justice - Examination moderateloop.ogg
goto rhea1I

# WRONG ANSWER
label TimeWaster
set _cr_back_button true
mus
witness
"{sfx/Smack.ogg}{s 10 10}C'mon, Apollo.{p20} That's no{n}reason for Erlenmeyer not{n}to be wearing gloves."
judge
emo serious name=jc
"Please don't waste the{n}court's time."
sfx /Bwaaah.ogg
penalty -20
is penalty 0 gameover
judge
emo normal name=jc
"What happened next, Ms. Wits?"
mus 111 - Apollo Justice - Examination moderate.ogg
set _music_loop 111 - Apollo Justice - Examination moderateloop.ogg
goto rhea1I
# FOUND THE CONTRADICTION

label DontBelieveIt
defense
emo handsondesk-nobrac name=dc
"I {f}{s 8 8}don't buy that Erlenmeyer{n}just took off gloves in the{n}middle of this!"
prosecutor
"Do you have evidence to the{n}contrary?"
defense
emo normal-nobrac name=dc
"Well, no, but--{p20}"
prosecutor
"Then {sfx/Smack.ogg}{s 10 10}shut up and let the witness{n}continue."
goto rhea1I

label press rhea1I
fg HoldItAnimApollo
pause 5 script
defense
emo normal-nobrac name=dc
"Isn't it convenient that the{n}'killer' {sfx/Ding.ogg}{f}passed out due to his{n}own actions?{p20} I couldn't help but"
"notice this is {f}{s 10 10}exactly what the{n}prosecution said in that trial{n}two days ago!"
witness
emo normal name=wc
"But that time it was speculation,{n}and this time I {f}{s 8 8}saw it with my{n}own two eyes!"
"I was {f}shocked, but also relieved.{p20}{n}After all..."
resume

label press rhea1J
fg HoldItAnimApollo
pause 5 script
defense
emo handsondesk-nobrac name=dc
"If the alarm was going off and{n}the police were about to arrive,{n}{sfx/Smack.ogg}{s 8 8}why didn't you stay to give"
"your testimony?!"
prosecutor
emo normal name=pc
"Perhaps from a moral and legal{n}point of view that is what she{n}should have done, but {s 8 8}what if"
"Erlenmeyer had woken up?{p30} {f}Dead men{n}tell no tales. {p20}{eserious}Would you prefer{n}that she'd have been killed?"
defense
emo sheepish-nobrac name=dc
"{c089}(...{p30}{f}Better not answer that.)"
witness
emo normal name=wc
"She's right...{p30} I'm not proud of{n}it, but I put my life before the{n}investigation. I was too afraid"
"Erlenmeyer would wake up and{n}{sfx/Slash.ogg}{f}{s 10 10}kill me! And so..."
resume

label press rhea1K
fg HoldItAnimApollo
pause 5 script
mus
defense
emo sheepish-nobrac name=dc
"R-{sfx/Ding.ogg}{f}{s 5 5}Really?"
witness
emo smiling1 name=wc
"That's right, Polly.{p20} You were my{n}{sfx/Shing.ogg}{f}{s 8 8}knight in shining armor!{p20} Just{n}seeing you saved me from the"
"trauma of what I had seen,{n}Polly...{p30} {esad}Just {f}{sn 8 8}seeing you reminded{n}me why I didn't want to die!"
defense
"Oh,{p15} uh,{p15} that's...{p30}{n}{sfx/Shock.ogg}{s 10 10}{eohshit-nobrac}{p15}{esweats-nobrac}{c089}(Gah! Don't fall for it again!){c}{n}{epointing-nobrac}{s 10 10}That's {s}complete nonsense!"
judge
"{s 10 10}Settle down, you two.{p20} You can{n}squabble once this trial is over.{n}...{p30}{sfx/Ding.ogg}{f}If it's ever over..."
mus 111 - Apollo Justice - Examination moderate.ogg
set _music_loop 111 - Apollo Justice - Examination moderateloop.ogg
goto comments

# COMMENTS BETWEEN CROSS LOOPS
label comments
defense
emo normal-nobrac name=dc
set _speaking dc
"{c089}(I now know what Rhea really{n}did...{p30} But {f}{s 5 5}why? {esweats-nobrac}{f}{s 5 5}How could I have{n}been so deceived?)"
"{c089}(I met her three days ago, and I{n}was {f}{s 5 5}trusting her like I had known{n}her for years!{p30} That'll teach me...)"
"{c089}{enormal-nobrac}(But now I need to concentrate on{n}completely {s 10 10}breaking her testimony,{n}{s 10 10}exposing her for what she truly is!)"
"{c089}(For Judge...{p30} and for {f}everyone{n}else this devil has harmed!)"
goto start

# AFTER FINDING 3 CONTRADICTIONS
label OnceMoreWithFeeling
clearcross
mus
defense
emo normal-nobrac name=dc
"{sfx/Table Slam.ogg}{edeskslam-nobrac}{p15}{ehandsondesk-nobrac}Your Honor!"
judge
emo shocked name=jc
"Y-{p15}Yes?"
defense
emo normal-nobrac name=dc
"Although this testimony does not{n}have any decisive contradictions...{p30}{n}Aren't {sfx/Dunnn.ogg}{f}{s 10 10}three inconsistencies"
"enough to cast {f}doubt on Ms. Wits?"
judge
emo normal name=jc
"Perhaps...{p30}{n}But, {sfx/Ding.ogg}{f}what should we do about it?"
mus 116 - Apollo Justice - Suspense.ogg
set _music_loop 116 - Apollo Justice - Suspenseloop.ogg
defense
emo normal-nobrac name=dc
"{sfx/Shing.ogg}{s 10 10}{eobjects-nobrac}{p7}{epointing-nobrac}Get a second testimony!{p30} A {f}detailed{n}testimony about the {s 10 10}actual moment{n}of the crime!"
"{enormal-nobrac}I think this will let the court{n}determine if she saw the crime...{p30}{n}or {sfx/Key.ogg}{f}committed it."
judge
"I suppose you have thoroughly{n}examined this first testimony..."
"And a second testimony will help{n}her credibility.{p30} Ms. Wits, you{n}have heard the defense's request:"
"please testify about the moment{n}in which the defendant ran over{n}the victim!"
mus
witness
"{delay2}...Fine."
script scene101rheatestimony2

# TOO MANY PENALTIES
label gameover
script gameover

# PRESENT SOMETHING WRONG
label none
mus 
prosecutor
emo normal name=pc
"{sfx/Bwaaah.ogg}{s 10 10}Blatherskite!"
defense
emo normal-nobrac name=dc
"Uh...{p20} {sfx/Key.ogg}{f}what?"
prosecutor
emo sarcastic name=pc
"I thought if you were going to{n}throw out some {f}random nonsense,{n}I'd try it too."
judge
emo normal name=jc
"Please don't waste the court's{n}time, Mr. Justice."
sfx /Bwaaah.ogg
penalty -25
is penalty 0 gameover
"What happened next, Ms. Wits?"
mus 111 - Apollo Justice - Examination moderate.ogg
set _music_loop 111 - Apollo Justice - Examination moderateloop.ogg
resume