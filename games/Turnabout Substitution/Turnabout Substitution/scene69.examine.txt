#-version-1
label top
bg ts/2cemeterycrimescene

examine
region 448 88 63 69 JudgeCar
region 427 144 43 48 carTracks
region 339 156 9 8 snowPile
region 152 108 17 23 graveA
region 322 68 74 56 graveB
region 299 61 21 26 graveC
region 294 89 33 47 ArthurGrave
region 477 161 29 31 body
menu scene69

#EXAMINE CAR FIRST TIME
label JudgeCar
flag SeenCar SeenCar
char Apollo hide
"So this is Judge's car...{n}technically, the murder weapon."
"From the position of the car and{n}the body, it seems that the murderer{n}had to run over Judge backwards."
"Makes sense so far..."
char game hide
addev judgescar
ev judgescar name=judgescar x=185
char game hide
nt
"{c099}{sfx\evidence.wav}Chambers' Car added to Court{n}Record."
delete name=judgescar
char Apollo hide
"I wonder why the car was parked{n}here, between the graves, in{n}the first place."
"It's not like it's an{n}authorized parking spot..."
"Maybe it was dark,{n}and Judge couldn't tell?"
"Hmm... H-Hey!{sfx\Ding.ogg} The {sfx/Key.ogg}{f}{c900}rear-view mirror{c}{n}is broken!"
"It must have been difficult to back{n}up... Yeah, I can see that{n}from the tracks."
"Anyway, there might be more to this{n}car than meets the eye. Where{n}should I check?"
setflag SeenCar
goto SuperTimes
#JUMP TO SUPERTIMES


#EXAMINE CAR SECOND TIME
label SeenCar
char Apollo hide
"Judge's car... The rear-view mirror{n}is broken. What should I{n}check out?"
goto SuperTimes
#JUMP TO SUPERTIMES


#SUPERTIMES
label SuperTimes

#OPTIONS THE FRONT THE BACK THE TRUNK
list CarOptions
li The Back
li The Trunk
li The Front
showlist
goto top

#THE BACK
label The Back
char Apollo hide
"Hm... Some snacks, a few loose{n}pieces of paper... Nothing out of{n}the ordinary or important."
goto SuperTimes

#THE TRUNK
label The Trunk
char Apollo hide
"Let's look inside the trunk...{n}{c900}{s}{sfx\Shock.ogg}Flowers{c}?"
"...If there was anything really{n}interesting, the police probably{n}already took it."
goto SuperTimes

#THE FRONT
label The Front
char Apollo hide
"Let's get a good look at the{n}front of the car..."
#ZOOM INTO FRONT
bg ts/carinterior stack fade
JudgesCar

#----- Judge's Car Zoom -----#
macro JudgesCar
label examineCar
bg ts/carinterior
examine
region 198 167 59 20 buttons
region 146 108 62 66 speedometer
region 0 0 256 71 windshield
region 3 69 157 123 steeringwheel
script scene69menu

#EXAMINE BUTTONS
label buttons
char Apollo hide
"The controls for the GPS are here...{n}I can't use it without the keys,{n}which I'm sure the police took."
goto examineCar

#EXAMINE SPEEDOMETER
label speedometer
char Apollo hide
"Let's see, the odometer says...{n}about 41300 miles."
"And the speedometer...{n}0 mph. Amazing."
goto examineCar

#EXAMINE WINDSHIELD
label windshield
char Apollo hide
"...Maybe it's just me, but the sky{n}looks even more depressing{n}from in here."
goto examineCar

#EXAMINE STEERING WHEEL
label steeringwheel
char Apollo hide
"The steering wheel... {sfx\Ding.ogg}{f}Wait!"
"If the killer used the car to run{n}over Judge, then his fingerprints{n}must be on the steering wheel!"
"It's the equivalent of having{n}his fingerprints on the{n}murder weapon!"
"I'd better remember to{n}ask Ema about this..."
flag SteeringWheelNoted examineCar
char game hide
nt
addev steeringwheel
ev steeringwheel
"{c099}{sfx\evidence.wav}Mental note of Steering Wheel made."
setflag SteeringWheelNoted
goto examineCar

#EXAMINE NOTHING
label none
char Apollo hide
"No clues here."
goto examineCar
endmacro
#--- End Judge's Car Zoom ---#

#EXAMINE CAR TRACKS
label carTracks
char Apollo hide
"These tracks in the snow run{n}all the way to the body..."
"I guess there's no doubt that's{n}how he died. But there's{n}so many tracks..."
"Did he run over Chambers a bunch{n}of times? ...He had{n}to be a reasonably skilled driver."
"He had to turn a bit while{n}reversing, since the body wasn't{n}in line with the car..."
"Why didn't he just{n}move the body?"
"I guess he was in a{n}hurry from the alarm..."
goto top

#EXAMINE SNO PILE
label snowPile
char Apollo hide
"Hey, I wonder what's under here...{sfx\Hush.ogg}"
"Chewing gum? One is lemon,{n}one is strawberry..."
"Someone must have dropped{n}these when they visited{n}the cemetery."
"{sfx\Ding.ogg}{f}Weird... I've never seen{n}a box of gum with both{n}strawberry and lemon flavors."
"I guess there's no harm{n}in keeping these."
flag gotGum top
char game hide
nt
addev gum
ev gum name=gun x=185
"{c099}{sfx\evidence.wav}Pieces of Gum added to Court{n}Record."
delete name=gum
setflag gotGum
goto top

#EXAMINE GRAVE A
label graveA
char Apollo hide
"'Here lies Dustin Dewinn, since{n}there isn't enough room to sit.'"
"...Well, I guess it's true."
goto top

#EXAMINE GRAVE B
label graveB
char Apollo hide
"Oh, this one has a poem..." #SILENT OR BEAUTIFUL MUSIC
mus
"{c090}Free your body and soul,{n}Unfold your powerful wings,{n}Climb up the highest mountains,"
"{c090}Kick your feet up in the air.{n}You may now live forever,{n}Or return to this earth,"
"{c090}Unless you feel good where you are!"
pause 120
"..."
bg ts\2cemeterycrimescene
"...I was expecting something{n}stupid or funny."
mus 123 - Apollo Justice - Search Opening.ogg
goto top

#EXAMINE GRAVE C
label graveC
char Apollo hide
"'Sue Flay... She lived with her{n}husband fifty years, and died in the{n}confident hope of a better life.'"
"{c099}(...Note to self: Ensure cemetery in{n}which I am buried is not{n}joke-based.)"
goto top

#EXAMINE ARTHUR'S GRAVE
label ArthurGrave
char Apollo hide
"Let's see what pun is{n}on this grave..."
"'Here lies... {c900}{f}{sfx\Slash.ogg}Arthur Chambers{c}.'"
"{sfx\Shock.ogg}{s}Aaaaah!! This must be why Judge{n}came to the cemetery!"
"...Could this be the 'urgent{n}matter' he mentioned after{n}the trial?"
"And if it was, why{n}did he come in the{n}middle of the night?"
"Curiouser and curiouser..."
goto top

#EXAMINE BODY
label body
char Apollo hide
"Judge's outline, in chalk."
"I know the police need to preserve{n}the crime scene and they can't just{n}keep the body here, but still..."
"I don't like it.{n}It's a constant reminder{n}of his death..."
"like he's been killed{n}once more."
goto top

#EXAMINE NOTHING
label none
char Apollo hide
"No clues here."
goto top