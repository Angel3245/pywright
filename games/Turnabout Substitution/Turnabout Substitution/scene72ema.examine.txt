#-version-1
label top
bg ts/biologyclassroom

examine
region 142 132 25 13 sheetmusic
region 186 135 37 57 photos
region 103 49 41 28 mice
region 0 87 143 105 tables
region 55 12 186 39 blackboard
menu scene72ema

label sheetmusic
char Ema
char Apollo hide
"Here it is... the sheet of{n}music signed 'Charles Darwin.'"

char Ema
"Let me see. Hmm... It seems{n}exactly the same as the others,"
"which would mean this{n}crime scene is the real deal."

flag musicAdded top
char game hide
nt 
addev sheetmusic
ev sheetmusic
"{sfx/evidence.wav}{c099}Sheet Music added{n}to the Court Record."
setflag musicAdded

flag musicAdded AND miceAdded AND talked2 AND talked3 EmaLeaves
goto top

label photos
char Ema
char Apollo hide
"Ugh, these fake naked{n}pictures... No wonder they weren't{n}disclosed to the public!"
"Ugh, I don't really want to..!{n}W-Wait, these aren't pictures{n}of Judge..."

char Ema e=whoa
"{sfx/Smack.ogg}{s}What?! {emu}Let me see!"

char Apollo hide
"These are pictures of {sfx/Owned.ogg}{f}Erlenmeyer!"
mus 10 - Suspense.ogg
set _music_loop 10 - Suspenseloop.ogg

char Ema e=mu
"Impossible! They're supposed{n}to be pictures of the victim!"

char Apollo hide
"What does this mean?"

char Ema e=profile
"...I can think of two explanations.{n}One, Erlenmeyer planned to kill{n}himself, or two, this was set up"
"by an imposter... {emu}at an extremely{n}unfortunate time."

char Apollo hide
"{c099}(Or three, Erlenmeyer is innocent!{n}I think this is the confirmation{n}I've been looking for!"
"{c099}It's good to know I was right{n}to trust my gut feeling...)"

noflag photosAdded addPhotos
label doneAddingPhotos

mus
char Ema
"...I'm going to guess that{n}this was an imposter."

char Apollo hide
"{c099}(I should play along with that{n}for now...)"
"But then how do they know all{n}the details of the crime scene?"
"The imposter would need to{n}work in law enforcement."

char Ema e=whoa
"{sfx/Shock.ogg}!{p30}{n}{ehair}...We'll deal with the identity{n}of the imposter later."
"{enormal}If we can confirm that this was{n}the work of an imposter, then it{n}will be irrelevant to the crime,"
"and we won't need to know who{n}exactly did this."
goto top

label addPhotos
char game hide
nt 
addev erlenmeyerphotos
ev erlenmeyerphotos name=ugh x=185
"{sfx/evidence.wav}{c099}Photos of Erlenmeyer added to{n}the Court Record (face-down)."
setflag photosAdded
delete name=ugh
goto doneAddingPhotos

label mice
char Ema
char Apollo hide
"Hey, what's with these mice?{n}They're not doing anything,{n}and look kinda... yellow."
"Maybe they haven't been{n}taking care of them properly?"

char Ema e=mu
"N-No, it's got to be more than{n}that... I've never seen anything{n}like this!"
"{eprofile}It looks like they're{n}sick with something..."

char Apollo hide
"...Do you think they are{n}related to the case?"

char Ema
"I don't know.{n}But I might as well take one{n}down to the lab to be sure."

flag miceAdded top
char game hide
nt
addev mice
ev mice
"{sfx/evidence.wav}{c099}Laboratory Mice added to{n}the Court Record."
setflag miceAdded

flag musicAdded AND miceAdded AND talked2 AND talked3 EmaLeaves
goto top

label tables
char Apollo hide
"{c099}(It must have been hard to{n}arrange these pictures into{n}the shape of a bust with all"
"{c099}the tables in the way...{n}Of course, that didn't stop...{n}whoever did this.)"
goto top

label blackboard
char Apollo hide
"{c099}(That blackboard, every student's{n}nightmare... or at least mine.{n}Even when someone else went up"
"{c099}there I would get nervous!{n}I guess that was the{n}Gramarye blood at work...)"
goto top

label EmaLeaves
char Ema
"Well, I think I've found{n}everything I'm going to find.{n}I'm going back to the station..."
"I'll let you know if I find{n}anything interesting about this{n}evidence, Apollo."
"Thanks for helping me investigate."
char Apollo hide
"No problem! See you later, Ema."
"{c099}(Hm... I don't think I'm going to{n}find anything else here, either.)"
setflag EmaGone
menu scene72

label none
char Apollo hide
"No clues here."
goto top