#-version-1
set _list_back_button false
set wrongevidence 1

label court
clearcourt
set _defense Apollo
set _prosecutor Poole
set _witness Ema
set _judge G.Strings
set _helper Rhea
startcourt
mus 111 - Apollo Justice - Examination moderate.ogg
set _music_loop 111 - Apollo Justice - Examination moderateloop.ogg
witness
"We found the defendant{n}unconscious in the{n}victim's prison cell."
"A gun was next to Chambers{n}when we found him."
emo profile name=wc
"We later proved this gun{n}was the murder weapon."
emo uninterested name=wc
"We know Erlenmeyer is not alive,{n}even if we don't have his body."
emo mu name=wc
"The bullet discovered at{n}the scene indicates he was{n}shot in the forehead."
"We also found traces of blood{n}on both the wall and bed."
emo glasses name=wc
"Genetic testing established{n}that it was the victim's blood."
emo profile name=wc
"The evidence and testimony{n}all pointed at Chambers,{n}so we had to arrest him."
mus
judge
"Thank you. That testimony{n}was very informative."
"Now, Mr. Justice, you may{n}begin your cross-examination."
witness
fg Cross-ExaminationAnim fade
nt
"{n}{c900}{center}-- Why Arrest Chambers? --"
mus 111 - Apollo Justice - Examination moderate.ogg
set _music_loop 111 - Apollo Justice - Examination moderateloop.ogg
label start
startcourt
cross ematestimony1

statement ema1A
witness
"We found the defendant{n}unconscious in the{n}victim's prison cell."

statement ema1B
witness
"A gun was next to Chambers{n}when we found him."

statement ema1C
witness
emo profile name=wc
"We later proved this gun{n}was the murder weapon."

statement ema1D
witness
emo uninterested name=wc
"We know Erlenmeyer is not alive,{n}even if we don't have his body."

statement ema1E
witness
emo mu name=wc
"The bullet discovered at{n}the scene indicates he was{n}shot in the forehead."

statement ema1F
witness
"We also found traces of blood{n}on both the wall and bed."

statement ema1G
witness
emo glasses name=wc
"Genetic testing established{n}that it was the victim's blood."

statement ema1H
emo profile name=wc
"The evidence and testimony{n}all pointed at Chambers,{n}so we had to arrest him."
endcross

label comments
helper
emo aide name=hc
set _speaking dc
"There's so much evidence{n}against Chambers..."
helper
emo aideserious name=hc
"It may be an uphill battle,{n}but it's worth it!"
set _speaking dc
"Yeah, you're right."
helper
emo aideserious name=hc
"The testimony just lists the{n}bare evidence found. This is{n}the prosecutor's basic argument!"
"If you can't crack this,{n}we're already sunk!"
set _speaking dc
"Ugh..."
helper
emo aide name=hc
"{sfx/Ding.ogg}{f}Oh, don't worry!{n}I'm sure you can do it.{n}Just try your best!"
goto start

label press ema1A
fg HoldItAnimApollo
pause 5 script
scrolldefense
"Wasn't there someone{n}else, Mr. Fisher,{n}in that cell, too?"
fg ObjectionAnimPoole
pause 5 script
scrollprosecutor
"Are you trying to pin the blame{n}on him? You must be desperate{n}if you're doing that already."
scrolldefense
emo sheepish name=dc
"Well..."
scrollprosecutor
"Either way, you're going to{n}need evidence if you{n}want to accuse someone."
judge
"Do you have evidence, Mr.Justice?"
defense
emo sweats name=dc
"N-{sfx/oops.wav}No..."
prosecutor
"Evidence is essential. You will{n}need to use it effectively{n}if you wish to advance."
defense
emo sweats name=dc
"I-I already know that..."
prosecutor
emo sarcastic name=pc
"Well it sure didn't seem that way."
emo normal name=pc
"In any case, the important{n}piece of information here is{n}that the defendant was"
"apprehended at the crime{n}scene. Ms. Skye, what else{n}did you find there?"
resume

label press ema1B
fg HoldItAnimApollo
pause 5 script
scrolldefense
"Can anyone confirm this?"
scrollwitness
emo hiya name=wc
"Just... {sfx/Ding.ogg}{f}the entire investigation team!"
scrolldefense
"...Oh. But what if someone planted{n}the gun to frame Chambers?"
fg ObjectionAnimPoole
pause 5 script
scrollprosecutor
emo serious name=pc
"If you'd done your homework,{n}you'd know the defendant brought{n}the gun with him, Antennas!"
"I have two witnesses to confirm this."
scrolldefense
emo sweats name=dc
"Uh..."
prosecutor
emo sarcastic name=pc
"Oh, and {sfx/Ding.ogg}{f}thanks for letting me{n}bring that tidbit to light."
scrolldefense
emo sweats name=dc
"{sfx/oops.wav}..."
helper
emo aide name=hc
"Hey, who would want to{n}frame Chambers anyway?"
set _speaking dc
"Well, we don't know anyone{n}with a personal motive."
"In all likelihood, it would be a judge.{n}With Chambers out of the way--{p20}{next}"
judge
emo serious name=jc
"{sfx/Bwaaah.ogg}{s 30 30}GUILTY!"
defense
emo ohshit name=dc
"{sfx/Shock.ogg}{s 15 15}Wha--{p20}{next}"
judge
emo serious name=jc
"{sfx/Bwaaah.ogg}{s 30 30}Guilty! Guilty! {sfx/Gavel.ogg}{f}Guilty!{n}Guilty! {sfx/Owned.ogg}{f}Guilty!"
defense
emo ohshit name=dc
"{sfx/Shock.ogg}{s 15 15}Y-Your Honor!!{p20}{next}"
judge
"{sfx/Smack.ogg}{s 10 10}I'm innocent! {sfx/Bwaaah.ogg}{s 30 30}INNOCENT!{n}{sfx/Smack.ogg}{s 10 10}Why doesn't anyone believe me?{n}{sfx/Bwaaah.ogg}{s 30 30}Innocent!"
defense
emo sheepish name=dc
"Your Honor, it's okay!{n}I'm not accusing you!!"
judge
"...{sfx/oops.wav}Good. Never mind then."
helper
emo aide name=hc
"Well looking at judges, we{n}know Chambers is innocent, and{n}that guy obviously didn't do it."
set _speaking dc
"But that only leaves..."
helper
emo aideserious name=hc
"{sfx/Key.ogg}{f}{c900}Paul Strings{c}."
set _speaking dc
"But he's the Chief Justice, meaning{n}it would help Chambers to{n}frame Strings, not the opposite..."
"Let's just focus on this{n}testimony for now."
resume

label press ema1C
fg HoldItAnimApollo
pause 5 script
scrolldefense
"How did you come to this{n}conclusion... {esheepish}{sfx/Key.ogg}{f}scientifically?
scrollwitness
emo glasses name=wc
"The gun was fired twice."
"Since the murderer missed the{n}first shot, we compared the bullet{n}found at the scene of the crime"
"with a test shot from the gun.{n}Scientifically speaking...{n}{esmirks}{sfx/Ding.ogg}{f}it was a perfect match!"
scrollprosecutor
"Proving that gun is{n}indeed the murder weapon."
judge
emo thinking name=jc
"But how do you know the{n}victim was really shot that night?"
resume

label press ema1D
fg HoldItAnimApollo
pause 5 script
scrolldefense
"How did the killer dispose{n}of the victim's body?"
fg ObjectionAnimPoole
pause 5 script
scrollprosecutor
"Weren't you listening?{n}{sfx/Dunnn.ogg}{s 10 10}We haven't found the corpse yet!{n}There's no way of knowing."
scrolldefense
emo confident name=dc
"I was asking for Ms. Skye's {sfx/Key.ogg}{f}{c900}opinion{c}."
judge
"Objection overruled.{n}Please give your opinion{n}on this matter, Detective."
fg ObjectionAnimPoole
pause 5 script
prosecutor
"Is there really any place{n}for conjecture and opinions{n}in a court of law?"
judge
emo serious name=jc
"Actually, in this case, {sfx/Bwaaah.ogg}{s 20 20}there is!"
prosecutor
"{sfx/Shock.ogg}{s 20 20}!"
judge
emo embarrassed name=jc
"... Uh... I mean... if you have{n}absolutely no idea where the body is,"
"you have a big hole in your {n}argument..."
defense
emo sweats name=dc
set _speaking dc
"{c099}(I don't know which is more pathetic...{n}the fact that we were so surprised{n}that he enforced his authority,"
"{c099}(or that he felt obligated{n}to justify it...)"
scrollwitness
emo profile name=wc
"Well, Clooney Prison{n}isn't that big."
"If the body was kept there,{n}I'm sure we would've{n}found it by now."
emo mu name=wc
"However, the prison is surrounded{n}by a deep, dense forest."
"It would be natural to{n}conceal a corpse there."
defense
emo thinking name=dc
set _speaking dc
"{c099}(So the corpse is in the forest{n}surrounding Clooney Prison...{n}Does that make sense?)"
list emasexplanation
li Of course!
li Not really...
showlist
label Of course!
defense
emo thinking name=dc
set _speaking dc
"{c099}(Yeah... it does seem{n}natural to do that.)"
clear
goto start
label Not really...
defense
"Actually, it may have been{n}natural to hide the{n}corpse in the forest."
"However, if that were{n}the case, there would be a{n}big {f}{sfx/Key.ogg}{c900}contradiction{c}."
scrollprosecutor
"And just what does it contradict?"
list contradictswhat
li The evidence
li Your testimony
showlist
label The evidence
defense
emo handsondesk name=dc
"The evidence! What else?"
scrollprosecutor
emo sarcastic name=pc
"Oh really? Then may I{n}see this evidence?"
set stupidbluff 1
set _cr_back_button false
present
label Your testimony
mus
defense
emo deskslam name=dc
sfx /deskslam.wav
pause 30 script
emo handsondesk name=dc
"The testimony! {edocument}You believe the{n}defendant disposed of the{n}victim's body in the"
"forest surrounding Clooney's{n}Prison, correct?"

prosecutor
"She was clear enough."
defense
emo confident name=dc
"And where was the defendant found?"
scrollwitness
emo glasses name=wc
"In the victim's prison cell."
scrollprosecutor
"{sfx/Shock.ogg}{s 20 20}!"
defense
emo objects name=dc
mus 113 - Apollo Justice - Objection.ogg
set _music_loop 113 - Apollo Justice - Objectionloop.ogg
"Now, why would the murderer{n}{sfx/Dunnn.ogg}{s}{c900}return{c} to the scene of the{n}crime?! To get caught?"
scrollwitness
emo whoa name=wc
"{sfx/Slash.ogg}{s 20 20}!!!"
scrolldefense
emo handsondesk name=dc
"But that's not the only{n}contradiction, Ms. Skye."
scrollwitness
"What?!"
scrolldefense
emo confident name=dc
"Seeing the defendant isn't exactly{n}the strongest man around, I think{n}it is safe to assume that"
"if he were the murderer,{n}he would've dragged -- not{n}carried -- the victim away."
scrollprosecutor
emo serious name=pc
"That seems reasonable.{n}But what's your point?"
scrolldefense

emo pointing name=dc
"It contradicts {sfx/Shing.ogg}{f}this{n}piece of evidence...!"
set stupidbluff 1
set stupidbluffB 1
label firstcontradiction
set _cr_back_button false
present

label press ema1E
fg HoldItAnimApollo
pause 5 script
scrolldefense
"How can you know if the{n}victim's body is missing?"
scrollwitness
"With the bullet's location."
judge
"Could you please clarify?"
witness
emo profile name=wc
"Well, looking at the bullet's{n}location and assuming the victim{n}was standing at the time,"
"Chambers had to be aiming{n}at his forehead when{n}he fired the gun."
scrolldefense
emo deskslam name=dc
sfx /deskslam.wav
pause 30 script
emo handsondesk name=dc
"But that bullet missed!{n}This is nothing but conjecture."
scrollprosecutor
"While we don't have the bullet{n}from the victim, this is the{n}only explanation that fits."
scrollwitness
emo mu name=wc
"But that's not even everything!"
resume

label press ema1F
fg HoldItAnimApollo
pause 5 script
scrolldefense
"What if the blood was{n}present before the murder?"
scrollwitness
"Scientifically speaking,{n}it couldn't have been."
"First of all, the guards{n}would've noticed it."
scrolldefense
emo deskslam name=dc
sfx /deskslam.wav
pause 30 script
emo handsondesk name=dc
"You can't prove that!{n}Humans aren't perfect."
scrollwitness
emo glasses name=wc
"{sfx/Ding.ogg}{f}I wasn't finished. The other reason is{n}that the blood hadn't completely{n}dried yet when we got to the scene."
scrolldefense
emo sweats name=dc
"...{sfx/oops.wav}Oh."
scrollwitness
emo smirks name=wc
"Win by KO."
defense
emo thinking name=dc
set _speaking dc
"{c099}(So much for that theory...)"
resume

label press ema1G
fg HoldItAnimApollo
pause 5 script
scrolldefense
emo thinking name=dc
"Genetic testing?"
scrollwitness
emo glasses name=wc
"Yep. We had a sample{n}of Erlenmeyer's blood."
emo hiya name=wc
"We compared it with the{n}blood in the cell, and{n}it was a perfect match!"
scrollprosecutor
"Even more proof of what{n}happened in that cell."
scrollwitness
emo profile name=wc
"Taking everything into account..."
resume

label press ema1H
fg HoldItAnimApollo
pause 5 script
scrolldefense
"Was the witness testimony{n}that big of a deal?"
scrollprosecutor
emo sarcastic name=pc
"Considering it's {sfx/Key.ogg}{f}{c900}conclusive{c}... Yes."
scrollwitness
emo won't-share name=wc
"And the first thing that happened{n}when we arrived at Clooney's Prison{n}was the witness accusing Chambers!"
scrolldefense
emo thinking name=dc
"What if the accusation influenced{n}the subsequent investigation?"
scrollwitness
"Apollo, we collected all{n}the evidence at the scene."
"No matter how you look{n}at it, it points to the{n}defendant, doesn't it?"
defense
emo thinking name=dc
set _speaking dc
"{c099}(I hate to admit it, but yes...)"
resume

label bloodupdated
isnot stupidbluffB 1 none
is stupidbluffB 1 bloodcontradiction
label bloodcontradiction
set _cr_back_button true
fg TakeThatAnimApollo
pause 5 script
defense
emo pointing name=dc
"Although there was blood inside{n}the victim's cell, there was{n}no blood in the hallway!"
mus
"{sfx/Dunnn.ogg}{s 20 20}How do you explain that, Ms. Skye?"
scrollwitness
emo hair name=wc
"Well... uh...{n}scientifically speaking..."
fg ObjectionAnimPoole
pause 5 script
scrollprosecutor
emo serious name=pc
"Hmph. All you proved was that the{n}witness's assumption was wrong."
"There's still plenty of evidence{n}that points to the defendant!"
scrollwitness
emo smirks name=wc
"{sfx/Ding.ogg}{f}Exactly!"
"Besides, how do you explain the{n}{sfx/Key.ogg}{c900}traces of blood{c} we found{n}on the {c900}defendant's robe?{c}"
judge
"Blood on the defendant's clothes?"
"You didn't mention that{n}before - please add this{n}to your testimony."
witness
emo mu name=wc
"Of course, Your Honor."
script scene25ematestimony1

label none
isnot stupidbluff 1 wrongevidence
is stupidbluff 1 stupidbluff

label stupidbluff
isnot stupidbluffB 1 stupidbluffA
is stupidbluffB 1 stupidbluffB
label stupidbluffA
set _cr_back_button true
prosecutor
"Hmph. The only thing this proves{n}is that you were bluffing."
defense
emo handsondesk name=dc
"What? I'm not bluffing!"
prosecutor
"...In that case, this evidence{n}proves you're an idiot."
judge
emo sarcastic name=jc
"Either way, it doesn't illustrate{n}your point, Mr. Justice."
sfx /Bwaaah.ogg
penalty -25
defense
emo thinking name=dc
set _speaking dc
"{c099}(I guess I should've thought{n}about that a little more...)"
set stupidbluff 0
is penalty 0 gameover
clear
goto start
label stupidbluffB
judge
emo sarcastic name=jc
"...And here I thought you{n}were on to something."
sfx /Bwaaah.ogg
penalty -25
is penalty 0 gameover
defense
emo handsondesk name=dc
"{f}{sfx/Ding.ogg}W-Wait! I am! I... {esheepish}just got{n}a little confused, that's all.{n}Let me try that again..."
goto firstcontradiction

label wrongevidence
is wrongevidence 1 wrongevidence1
is wrongevidence 2 wrongevidence2
is wrongevidence 3 wrongevidence3

label wrongevidence1
fg ObjectionAnimApollo
pause 5 script
scrolldefense
emo objects name=dc
"{sfx/Dunnn.ogg}{s}Your Honor!{n}That statement contradicts{n}the evidence!"
judge
emo shocked name=jc
"...{sfx/Key.ogg}{f}Really? Let me see that..."
"...{sfx/Ding.ogg}{f}! Why, you're right!"
"This is... unheard of{n}in this courtroom!"
"You single-handedly turned{n}the case around with{n}this one piece of evidence!"
defense
emo confident name=dc
"Didn't I? Didn't I?"
judge
emo sarcastic name=jc
"...{sfx/oops.wav}Of course you didn't."
"What IS unheard of in this courtroom{n}is your credulity, Mr.Justice.{n}You deserve this penalty."
sfx /Bwaaah.ogg
penalty -25
defense
emo sweats name=dc
set _speaking dc
"{c099}(My ego... it hurts...)"
is penalty 0 gameover
set wrongevidence 2
clear
goto start

label wrongevidence2
fg ObjectionAnimApollo
pause 5 script
judge
emo serious name=jc
"{sfx/Dunnn.ogg}{s}Penalty!"
sfx /Bwaaah.ogg
penalty -25
defense
emo objects name=dc
"{sfx/Shing.ogg}{f}What?!{n}I haven't said anything yet!"
judge
"But you were definitely going{n}to say something{n}ridiculous, weren't you?"
defense
set _speaking dc
"{c099}(Actually, he's right...)"
judge
emo sarcastic name=jc
"Which is why I decided{n}to prevent you from{n}further embarrassing yourself."
"You should be grateful."
defense
emo sheepish name=dc
"Thank you, Your Honor."
set _speaking dc
"{c099}(Though I AM embarrassed now.)"
is penalty 0 gameover
set wrongevidence 3
clear
goto start

label wrongevidence3
fg ObjectionAnimApollo
pause 5 script
scrolldefense
"..."
emo sheepish name=dc
"{sfx/oops.wav}Please give me a penalty, Your{n}Honor. I swear I was going{n}to say something stupid."
judge
"...You sure do learn fast, Mr.Justice.{n}Alright, I'd be glad to."
sfx /Bwaaah.ogg
penalty -25
defense
emo sweats name=dc
set _speaking dc
"{c099}(I a-bso-lu-tely need{n}to remember to{n}forget that moment.)"
is penalty 0 gameover
set wrongevidence 1
clear
goto start

label gameover
mus
fg ani-judge-gavel
pause 30 script
clear
bg aj/judgeseat
char G.Strings
"...I believe we've reached{n}a conclusion."
"Members of the jury, you have{n}seen and heard all the evidence and{n}the arguments of the attorneys."
"Now I will instruct you on the law."
clear
pause 60
nt
"{c090}{center}Five hours later..."
bg aj/judgeseat
char G.Strings
"You may now retire{n}to deliberate."
clear
pause 60
nt
"{c090}{center}One minute later..."
bg aj/judgeseat
char G.Strings
"Members of the jury."
"By unanimous verdict, you have{n}found the accused...{next}"
guilty
"The defendant will surrender{n}himself to the court's care..."
"...to undergo a regular trial{n}at the High Court within a{n}month's time."
"Court is adjourned!"
fg ani-judge-gavel
pause 30 script
exit