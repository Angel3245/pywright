#-version-1
set _list_back_button false
set wrongevidence 1

label court
clearcourt
set _defense Apollo
set _prosecutor Poole
set _witness Ema
set _judge G.Strings
set _helper Rhea
mus 111 - Apollo Justice - Examination moderate.ogg
set _music_loop 111 - Apollo Justice - Examination moderateloop.ogg
label start
startcourt
cross ematestimony1

statement ema1A
witness
"We found the defendant{n}unconscious in the{n}victim's prison cell."

statement ema1B
witness
"A gun was next to Chambers{n}when we found him."

statement ema1C
witness
emo profile name=wc
"We later proved this gun{n}was the murder weapon."

statement ema1D
witness
emo uninterested name=wc
"We know Erlenmeyer is not alive,{n}even if we don't have his body."

statement ema1E
witness
emo mu name=wc
"The bullet discovered at{n}the scene indicates he was{n}shot in the forehead."

statement ema1F
witness
"We also found traces of blood{n}on both the wall and bed."

statement ema1G
witness
"We found the exact same blood{n}on the defendant's robe."

statement ema1H
witness
emo glasses name=wc
"Genetic testing established{n}that it was the victim's blood."

statement ema1I
emo profile name=wc
"The evidence and testimony{n}all pointed at Chambers,{n}so we had to arrest him."
endcross

label comments
helper
emo aide name=hc
set _speaking dc
"There's so much evidence{n}against Chambers..."
helper
emo aideserious name=hc
"It may be an uphill battle,{n}but it's worth it!"
set _speaking dc
"Yeah, you're right."
helper
emo aideserious name=hc
"The testimony just lists the{n}bare evidence found. This is{n}the prosecutor's basic argument!"
"If you can't crack this,{n}we're already sunk!"
set _speaking dc
"Ugh..."
helper
emo aide name=hc
"{sfx/Ding.ogg}{f}Oh, don't worry!{n}I'm sure you can do it.{n}Just try your best!"
goto start

label press ema1A
fg HoldItAnimApollo
pause 5 script
scrolldefense
"Wasn't there someone{n}else, Mr. Fisher,{n}in that cell, too?"
fg ObjectionAnimPoole
pause 5 script
scrollprosecutor
"Are you trying to pin the blame{n}on him? You must be desperate{n}if you're doing that already."
scrolldefense
emo sheepish name=dc
"Well..."
scrollprosecutor
"Either way, you're going to{n}need evidence if you{n}want to accuse someone."
judge
"Do you have evidence, Mr.Justice?"
defense
emo sweats name=dc
"N-{sfx/oops.wav}No..."
prosecutor
"Evidence is essential. You will{n}need to use it effectively{n}if you wish to advance."
defense
emo sweats name=dc
"I-I already know that..."
prosecutor
emo sarcastic name=pc
"Well it sure didn't seem that way."
emo normal name=pc
"In any case, the important{n}piece of information here is{n}that the defendant was"
"apprehended at the crime{n}scene. Ms. Skye, what else{n}did you find there?"
resume

label press ema1B
fg HoldItAnimApollo
pause 5 script
scrolldefense
"Can anyone confirm this?"
scrollwitness
emo hiya name=wc
"Just... {sfx/Ding.ogg}{f}the entire investigation team!"
scrolldefense
"...Oh. But what if someone planted{n}the gun to frame Chambers?"
fg ObjectionAnimPoole
pause 5 script
scrollprosecutor
emo serious name=pc
"If you'd done your homework,{n}you'd know the defendant brought{n}the gun with him, Antennas!"
"I have two witnesses to confirm this."
scrolldefense
emo sweats name=dc
"Uh..."
prosecutor
emo sarcastic name=pc
"Oh, and {sfx/Ding.ogg}{f}thanks for letting me{n}bring that tidbit to light."
scrolldefense
emo sweats name=dc
"{sfx/oops.wav}..."
helper
emo aide name=hc
"Hey, who would want to{n}frame Chambers anyway?"
set _speaking dc
"Well, we don't know anyone{n}with a personal motive."
"In all likelihood, it would be a judge.{n}With Chambers out of the way--{p20}{next}"
judge
emo serious name=jc
"{sfx/Bwaaah.ogg}{s 30 30}GUILTY!"
defense
emo ohshit name=dc
"{sfx/Shock.ogg}{s 15 15}Wha--{p20}{next}"
judge
emo serious name=jc
"{sfx/Bwaaah.ogg}{s 30 30}Guilty! Guilty! {sfx/Gavel.ogg}{f}Guilty!{n}Guilty! {sfx/Owned.ogg}{f}Guilty!"
defense
emo ohshit name=dc
"{sfx/Shock.ogg}{s 15 15}Y-Your Honor!!{p20}{next}"
judge
"{sfx/Smack.ogg}{s 10 10}I'm innocent! {sfx/Bwaaah.ogg}{s 30 30}INNOCENT!{n}{sfx/Smack.ogg}{s 10 10}Why doesn't anyone believe me?{n}{sfx/Bwaaah.ogg}{s 30 30}Innocent!"
defense
emo sheepish name=dc
"Your Honor, it's okay!{n}I'm not accusing you!!"
judge
"...{sfx/oops.wav}Good. Never mind then."
helper
emo aide name=hc
"Well looking at judges, we{n}know Chambers is innocent, and{n}that guy obviously didn't do it."
set _speaking dc
"But that only leaves..."
helper
emo aideserious name=hc
"{sfx/Key.ogg}{f}{c900}Paul Strings{c}."
set _speaking dc
"But he's the Chief Justice, meaning{n}it would help Chambers to{n}frame Strings, not the opposite..."
"Let's just focus on this{n}testimony for now."
resume

label press ema1C
fg HoldItAnimApollo
pause 5 script
scrolldefense
"How did you come to this{n}conclusion... {esheepish}{sfx/Key.ogg}{f}scientifically?
scrollwitness
emo glasses name=wc
"The gun was fired twice."
"Since the murderer missed the{n}first shot, we compared the bullet{n}found at the scene of the crime"
"with a test shot from the gun.{n}Scientifically speaking...{n}{esmirks}{sfx/Ding.ogg}{f}it was a perfect match!"
scrollprosecutor
"Proving that gun is{n}indeed the murder weapon."
judge
emo thinking name=jc
"But how do you know the{n}victim was really shot that night?"
resume

label press ema1D
fg HoldItAnimApollo
pause 5 script
scrolldefense
"So you don't have an{n}explanation for what{n}happened to the corpse."
scrollprosecutor
emo serious name=pc
"We've already been over this."
"What's important is the{n}cold, hard evidence proving{n}the defendant is guilty!"
resume

label press ema1E
fg HoldItAnimApollo
pause 5 script
scrolldefense
"How can you know if the{n}victim's body is missing?"
scrollwitness
"With the bullet's location."
judge
"Could you please clarify?"
witness
emo profile name=wc
"Well, looking at the bullet's{n}location and assuming the victim{n}was standing at the time,"
"Chambers had to be aiming{n}at his forehead when{n}he fired the gun."
scrolldefense
emo deskslam name=dc
sfx /deskslam.wav
pause 30 script
emo handsondesk name=dc
"But that bullet missed!{n}This is nothing but conjecture."
scrollprosecutor
"While we don't have the bullet{n}from the victim, this is the{n}only explanation that fits."
scrollwitness
emo mu name=wc
"But that's not even everything!"
resume

label press ema1F
fg HoldItAnimApollo
pause 5 script
scrolldefense
"What if the blood was{n}present before the murder?"
scrollwitness
"Scientifically speaking,{n}it couldn't have been."
"First of all, the guards{n}would've noticed it."
scrolldefense
emo deskslam name=dc
sfx /deskslam.wav
pause 30 script
emo handsondesk name=dc
"You can't prove that!{n}Humans aren't perfect."
scrollwitness
emo glasses name=wc
"{sfx/Ding.ogg}{f}I wasn't finished. The other reason is{n}that the blood hadn't completely{n}dried yet when we got to the scene."
scrolldefense
emo sweats name=dc
"...{sfx/oops.wav}Oh."
scrollwitness
emo smirks name=wc
"Win by KO."
defense
emo thinking name=dc
set _speaking dc
"{c099}(So much for that theory...)"
resume

label press ema1G
fg HoldItAnimApollo
pause 5 script
scrolldefense
emo handsondesk name=dc
"The defendant's robe?"
scrollwitness
emo uninterested name=wc
"That's right. Although{n}I have to say, I{n}really don't dig it."
judge
emo embarrassed name=jc
"{sfx/Ding.ogg}{f}H-Hey! I like the black robes!{n}They make one look dignified."
prosecutor
"In any case, traces of the{n}victim's blood were found{n}on the defendant's robe."
"That is what's important here."
resume

label press ema1H
fg HoldItAnimApollo
pause 5 script
scrolldefense
emo thinking name=dc
"Genetic testing?"
scrollwitness
emo glasses name=wc
"Yep. We had a sample{n}of Erlenmeyer's blood."
emo hiya name=wc
"We compared it with the{n}blood in the cell, and{n}it was a perfect match!"
scrollprosecutor
"Even more proof of what{n}happened in that cell."
scrollwitness
emo profile name=wc
"Taking everything into account..."
resume

label press ema1I
fg HoldItAnimApollo
pause 5 script
scrolldefense
"Was the witness testimony{n}that big of a deal?"
scrollprosecutor
emo sarcastic name=pc
"Considering it's {sfx/Key.ogg}{f}{c900}conclusive{c}... Yes."
scrollwitness
emo won't-share name=wc
"And the first thing that happened{n}when we arrived at Clooney's Prison{n}was the witness accusing Chambers!"
scrolldefense
emo thinking name=dc
"What if the accusation influenced{n}the subsequent investigation?"
scrollwitness
"Apollo, we collected all{n}the evidence at the scene."
"No matter how you look{n}at it, it points to the{n}defendant, doesn't it?"
defense
emo thinking name=dc
set _speaking dc
"{c099}(I hate to admit it, but yes...)"
resume

label uniform ema1G
mus
fg ObjectionAnimApollo
pause 5 script
scrolldefense
"{sfx/Key.ogg}{f}I'm afraid that could{n}not have happened, Ms. Skye."
scrollwitness
emo mu name=wc
"What are you talking about?"
scrolldefense
emo handsondesk name=dc
"If the defendant was actually the{n}murderer, the blood would have{n}got on the clothes he was wearing."
mus 113 - Apollo Justice - Objection.ogg
set _music_loop 113 - Apollo Justice - Objectionloop.ogg
sfx /evidenceshoop.wav
ev uniform name=uniform x=185
emo objects name=dc
"{f}And at the time, he was {f}wearing{n}this {sfx/Shing.ogg}{f}{c900}uniform{c}, not a {c900}robe{c}!"
delete name=uniform
scrollwitness
emo mu name=wc
"{sfx/Slash.ogg}{s 20 20}W-What?! B-But he was wearing{n}a robe when we found him!"
emo uninterested name=wc
"The entire investigation team{n}and I can confirm that."
scrolldefense
emo deskslam name=dc
sfx /deskslam.wav
pause 30 script
emo handsondesk name=dc
"The real murderer must have{n}planted that blood --{p10}{next}"
mus
fg ObjectionAnimPoole
pause 5 script
scrollprosecutor
"I must give you credit.{n}I didn't think you'd pick that up."
scrolldefense
"Huh? Oh... uh... {esheepish}thanks."
scrollprosecutor
"Of course, you're still only{n}a little red-horned greenhorn."
emo sarcastic name=pc
"After all...{p30} {f}{sfx/Key.ogg}you did{n}just do my job."
helper
emo aide name=hc
"Something tells me this{n}isn't going to be good..."
set _speaking dc
"{c099}(From the way Chambers described{n}her, it's never going to{n}be good with Poole...)"
prosecutor
"The traces of blood {sfx/Shing.ogg}{s 10 10}clear{n}up all other contradictions!"
mus 112 - Apollo Justice - Trance Logic.ogg
set _music_loop 112 - Apollo Justice - Trance Logicloop.ogg
"Here is what happened: Chambers{n}first came to Clooney Prison with{n}the guard uniform over his robe."
"He {sfx/gunshot.wav}{s}shot the victim in{n}the forehead, and used the{n}uniform to stop the blood."
"Although there was still{n}blood splatter, the uniform{n}prevented any blood from spilling"
"while depositing the body{n}in the nearby forest."
emo serious name=pc
"Afterwards he came back to{n}the cell to clean up the blood{n}and wipe his fingerprints."
"However, he realized the{n}enormity of what he had{n}done and passed out."
helper
emo aide name=hc
"That sounds like a{n}pretty solid argument."
set _speaking dc
"And there aren't{n}any contradictions..."
mus
prosecutor
"This is what we've gathered{n}from Ms. Skye's testimony:"
mus 10 - Suspense.ogg
set _music_loop 10 - Suspenseloop.ogg
"Robert Erlenmeyer is undoubtedly {n}dead and the defendant is{n}the primary suspect because"
"A, he was found on{n}the crime scene,"
"B, his fingerprints are{n}on the murder weapon,"
"C, there was blood on his clothes{n}and D, we have a witness."
helper
emo aide name=hc
"All this does sound convincing..."
judge
"I was about to say the same thing."
prosecutor
emo sarcastic name=pc
"Heh. I'm just getting started."
defense
emo sweats name=dc
set _speaking dc
"{c099}(Uh-oh...)"
mus
prosecutor
"In addition to all this overwhelming{n}evidence, the defendant had a{n}{sfx/Key.ogg}{f}{c900}crystal clear motive{c}."
"Ms. Skye, if you would{n}please testify about it..."
scrollwitness
emo hiya name=wc
"{sfx/Ding.ogg}{f}One motive, coming right up!"
script scene26


label none
is wrongevidence 1 wrongevidence1
is wrongevidence 2 wrongevidence2
is wrongevidence 3 wrongevidence3

label wrongevidence1
fg ObjectionAnimApollo
pause 5 script
scrolldefense
emo objects name=dc
"{sfx/Dunnn.ogg}{s}Your Honor!{n}That statement contradicts{n}the evidence!"
judge
emo shocked name=jc
"...{sfx/Key.ogg}{f}Really? Let me see that..."
"...{sfx/Ding.ogg}{f}! Why, you're right!"
"This is... unheard of{n}in this courtroom!"
"You single-handedly turned{n}the case around with{n}this one piece of evidence!"
defense
emo confident name=dc
"Didn't I? Didn't I?"
judge
emo sarcastic name=jc
"...{sfx/oops.wav}Of course you didn't."
"What IS unheard of in this courtroom{n}is your credulity, Mr.Justice.{n}You deserve this penalty."
sfx /Bwaaah.ogg
penalty -25
defense
emo sweats name=dc
set _speaking dc
"{c099}(My ego... it hurts...)"
is penalty 0 gameover
set wrongevidence 2
clear
goto start

label wrongevidence2
fg ObjectionAnimApollo
pause 5 script
judge
emo serious name=jc
"{sfx/Dunnn.ogg}{s}Penalty!"
sfx /Bwaaah.ogg
penalty -25
defense
emo objects name=dc
"{sfx/Shing.ogg}{f}What?!{n}I haven't said anything yet!"
judge
"But you were definitely going{n}to say something{n}ridiculous, weren't you?"
defense
set _speaking dc
"{c099}(Actually, he's right...)"
judge
emo sarcastic name=jc
"Which is why I decided{n}to prevent you from{n}further embarrassing yourself."
"You should be grateful."
defense
emo sheepish name=dc
"Thank you, Your Honor."
set _speaking dc
"{c099}(Though I AM embarrassed now.)"
is penalty 0 gameover
set wrongevidence 3
clear
goto start

label wrongevidence3
fg ObjectionAnimApollo
pause 5 script
scrolldefense
"..."
emo sheepish name=dc
"{sfx/oops.wav}Please give me a penalty, Your{n}Honor. I swear I was going{n}to say something stupid."
judge
"...You sure do learn fast, Mr.Justice.{n}Alright, I'd be glad to."
sfx /Bwaaah.ogg
penalty -25
defense
emo sweats name=dc
set _speaking dc
"{c099}(I a-bso-lu-tely need{n}to remember to{n}forget that moment.)"
is penalty 0 gameover
set wrongevidence 1
clear
goto start

label gameover
mus
fg ani-judge-gavel
pause 30 script
clear
bg aj/judgeseat
char G.Strings
"...I believe we've reached{n}a conclusion."
"Members of the jury, you have{n}seen and heard all the evidence and{n}the arguments of the attorneys."
"Now I will instruct you on the law."
clear
pause 60
nt
"{c090}{center}Five hours later..."
bg aj/judgeseat
char G.Strings
"You may now retire{n}to deliberate."
clear
pause 60
nt
"{c090}{center}One minute later..."
bg aj/judgeseat
char G.Strings
"Members of the jury."
"By unanimous verdict, you have{n}found the accused...{next}"
guilty
"The defendant will surrender{n}himself to the court's care..."
"...to undergo a regular trial{n}at the High Court within a{n}month's time."
"Court is adjourned!"
fg ani-judge-gavel
pause 30 script
exit